/*
 * Copyright IBM Corp. 2018 All Rights Reserved.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id "com.github.ben-manes.versions" version "0.53.0"
    id "com.diffplug.spotless" version "8.1.0"
    id "com.gradleup.nmcp.aggregation" version "1.4.3"
    id "com.gradleup.nmcp" version "1.4.3" apply false
}

version = '2.5.9'

// If the nightly property is set, then this is the scheduled main
// build - and we should publish this to artifactory
//
// Use the .dev.<number> format to match Maven convention
if (properties.containsKey('NIGHTLY')) {
    version = version + '.dev.' + getDate()
    ext.nightly = true   // set property for use in subprojects
} else {
    ext.nightly = false
}

nmcpAggregation {
    centralPortal {
        username = findProperty('mavenCentralUsername')
        password = findProperty('mavenCentralPassword')
        publishingType = "AUTOMATIC"
    }
}

dependencies {
    nmcpAggregation(project(':fabric-chaincode-shim'))
}

allprojects {
    apply plugin: "com.diffplug.spotless"

    repositories {
        mavenCentral()
    }

    spotless {
        format 'misc', {
            target '*.gradle', '.gitattributes', '.gitignore'
            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "maven-publish"

    group = 'org.hyperledger.fabric-chaincode-java'
    version = rootProject.version

    compileJava {
        options.release = 11
        options.compilerArgs += ['-Werror', '-Xlint:all']
    }

    dependencies {
        implementation 'commons-cli:commons-cli:1.11.0'
        implementation 'commons-logging:commons-logging:1.3.5'

        testImplementation platform('org.junit:junit-bom:6.0.2')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.assertj:assertj-core:3.27.6'
        testImplementation 'org.mockito:mockito-core:5.21.0'
        testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'

        testImplementation 'org.hamcrest:hamcrest-library:3.0'
    }

    test {
        useJUnitPlatform()
    }

    spotless {
        java {
            removeUnusedImports()
            palantirJavaFormat().formatJavadoc(true)
            formatAnnotations()
        }
    }
}

// Get the date in the reverse format for sorting
static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}
